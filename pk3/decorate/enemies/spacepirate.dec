ACTOR SpacePirate 17010
{
    Health 150
    PainChance "IceBeam", 96
    PainChance "IceCharged", 128
    PainChance "IceChargedExplode", 96
    PainChance "IceBeamExplode", 64
    PainChance 80
    Scale 0.75
    Speed 12
    Radius 24
    Height 56
    Gravity 0.5
    Mass 400
    BloodColor Green
    Monster
    +FLOORCLIP
    +MISSILEMORE
    SeeSound "pirate/see"
    PainSound "pirate/pain"
    DeathSound "pirate/death"
    ActiveSound "pirate/idle"
    DamageFactor "NoDamage", 0.0
    Obituary "%o was shot down by a Space Pirate."
    States
    {
    Spawn:
        PIRT A 10 A_Look
        Loop
    See:
        TNT1 A 0 A_ChangeFlag("PAINLESS",0)
        PIRT AABBCCDD 2 A_Chase
        Loop
    Missile:
        TNT1 A 0 A_Jump(32,"Missile3")
        TNT1 A 0 A_Jump(64,"Missile2")
        TNT1 A 0 A_JumpIfInventory("IceBeamChilled",1,"MissileChilled")
        PIRT E 4 A_FaceTarget
        TNT1 A 0 A_PlaySound("pirate/shot")
        TNT1 A 0 A_CustomMissile("PirateBolt",32,16,0,0,0)
        PIRT F 4 BRIGHT A_CustomMissile("PirateBolt",32,-16,0,0,0)
        PIRT ABCD 2 A_Wander
        TNT1 A 0 A_Jump(128,"See")
        TNT1 A 0 A_CPosRefire
        goto Missile
    MissileChilled:
        PIRT E 6 A_FaceTarget
        TNT1 A 0 A_PlaySound("pirate/shot")
        TNT1 A 0 A_CustomMissile("PirateBolt",32,16,0,0,0)
        PIRT F 6 BRIGHT A_CustomMissile("PirateBolt",32,-16,0,0,0)
        PIRT ABCD 3 A_Wander
        TNT1 A 0 A_Jump(128,"See")
        TNT1 A 0 A_CPosRefire
        goto Missile
    Missile2:
        TNT1 A 0 A_Jump(256,"StrafeLeft","StrafeRight")
    StrafeLeft:
        TNT1 A 0 ThrustThingZ(0,random(10,20),0,1)
	    TNT1 A 0 ThrustThing(angle*256/360+192, random(16,24), 0, 0)
	    goto Missile2B
    StrafeRight:
        TNT1 A 0 ThrustThingZ(0,random(10,20),0,1)
	    TNT1 A 0 ThrustThing(angle*256/360+64, random(16,24), 0, 0)
    Missile2B:
        TNT1 A 0 A_JumpIfInventory("IceBeamChilled",1,"Missile2Chilled")
        PIRT E 4 A_FaceTarget
        TNT1 A 0 A_PlaySound("pirate/shot")
        TNT1 A 0 A_CustomMissile("PirateBolt",32,16,0,0,0)
        PIRT F 4 BRIGHT A_CustomMissile("PirateBolt",32,-16,0,0,0)
        PIRT E 4 A_FaceTarget
        TNT1 A 0 A_PlaySound("pirate/shot")
        TNT1 A 0 A_CustomMissile("PirateBolt",32,16,0,0,0)
        PIRT F 4 BRIGHT A_CustomMissile("PirateBolt",32,-16,0,0,0)
        PIRT E 4 A_FaceTarget
        TNT1 A 0 A_PlaySound("pirate/shot")
        TNT1 A 0 A_CustomMissile("PirateBolt",32,16,0,0,0)
        PIRT F 4 BRIGHT A_CustomMissile("PirateBolt",32,-16,0,0,0)
        goto See
    Missile2Chilled:
        PIRT E 6 A_FaceTarget
        TNT1 A 0 A_PlaySound("pirate/shot")
        TNT1 A 0 A_CustomMissile("PirateBolt",32,16,0,0,0)
        PIRT F 6 BRIGHT A_CustomMissile("PirateBolt",32,-16,0,0,0)
        PIRT E 6 A_FaceTarget
        TNT1 A 0 A_PlaySound("pirate/shot")
        TNT1 A 0 A_CustomMissile("PirateBolt",32,16,0,0,0)
        PIRT F 6 BRIGHT A_CustomMissile("PirateBolt",32,-16,0,0,0)
        PIRT E 6 A_FaceTarget
        TNT1 A 0 A_PlaySound("pirate/shot")
        TNT1 A 0 A_CustomMissile("PirateBolt",32,16,0,0,0)
        PIRT F 6 BRIGHT A_CustomMissile("PirateBolt",32,-16,0,0,0)
        goto See
    Missile3:
        TNT1 A 0 ThrustThingZ(0,random(40,60),0,1)
        TNT1 A 0 A_Recoil(random(-5,-10))
        TNT1 A 0 A_ChangeFlag("PAINLESS",1)
    Missile3B:
        TNT1 A 0 A_JumpIfInventory("IceBeamChilled",1,"Missile3Chilled")
        PIRT E 4 A_FaceTarget
        TNT1 A 0 A_PlaySound("pirate/shot")
        TNT1 A 0 A_CustomMissile("PirateBolt",32,16,0,0,0)
        PIRT F 4 BRIGHT A_CustomMissile("PirateBolt",32,-16,0,0,0)
        TNT1 A 0 A_CheckFloor("See")
        goto Missile3B
    Missile3Chilled:
        PIRT E 6 A_FaceTarget
        TNT1 A 0 A_PlaySound("pirate/shot")
        TNT1 A 0 A_CustomMissile("PirateBolt",32,16,0,0,0)
        PIRT F 6 BRIGHT A_CustomMissile("PirateBolt",32,-16,0,0,0)
        TNT1 A 0 A_CheckFloor("See")
        goto Missile3Chilled
    Pain.IceCharged:
    Pain.IceChargedExplode:
    Pain.IceBeam:
    Pain.IceBeamExplode:
        TNT1 A 0 A_Jump(32,2)
        TNT1 A 0 A_JumpIfInventory("IceBeamChilled",1,"Pain")
        TNT1 A 0
        TNT1 AAAAAAAAAA 0 A_SpawnItemEx("IceTrailFlicker",random(-1,1),random(-1,1),random(16,46),random(-500,500)/100.00,random(-500,500)/100.00,random(-200,500)/100.00,random(-180,180),161)
        TNT1 AAAAA 0 A_SpawnItemEx("IceTrailFlicker3",random(-1,1),random(-1,1),random(16,46),random(-400,400)/100.00,random(-400,400)/100.00,random(10,500)/100.00,random(-180,180),161)
        TNT1 A 0 ACS_ExecuteAlways(594,0,8,0,0)
        TNT1 A 0 A_GiveInventory("IceBeamChilled",1)
        TNT1 A 0 A_ChangeFlag("PAINLESS",1)
        PIRT Z 70
        TNT1 A 0 A_ChangeFlag("PAINLESS",0)
    Pain:
        TNT1 A 0
        TNT1 A 0 ThrustThingZ(0,random(15,30),0,1)
        TNT1 A 0 A_Recoil(random(4,8))
        PIRT A 2 A_Pain
        Goto See



    Death:
        TNT1 A 0
        TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(594,10,0,0) == 1, "DeathNope")
        TNT1 A 0 A_JumpIfInTargetInventory("SuperMissileAcquired",1,"DeathSuperMissile")
        TNT1 A 0 A_JumpIfInTargetInventory("PowerBombAcquired",1,"DeathPowerBomb")
        TNT1 A 0 A_SpawnItemEx("EnemyDropSpawner",0,0,10,0,0,0,0,32)
    DeathNope:
        TNT1 A 0
        TNT1 A 0 A_JumpIfInventory("EnemyFlyingDeath",1,"DeathMissile3")
        goto DeathAnimation
    DeathSuperMissile:
        TNT1 A 0 A_JumpIfInTargetInventory("PowerBombAcquired",1,"DeathBoth")
        TNT1 A 0 A_SpawnItemEx("EnemyDropSpawnerB",0,0,10,0,0,0,0,32)
        TNT1 A 0 A_JumpIfInventory("EnemyFlyingDeath",1,"DeathMissile3")
        goto DeathAnimation
    DeathPowerBomb:
        TNT1 A 0 A_SpawnItemEx("EnemyDropSpawnerC",0,0,10,0,0,0,0,32)
        TNT1 A 0 A_JumpIfInventory("EnemyFlyingDeath",1,"DeathMissile3")
        goto DeathAnimation
    DeathBoth:
        TNT1 A 0 A_SpawnItemEx("EnemyDropSpawnerD",0,0,10,0,0,0,0,32)
        TNT1 A 0 A_JumpIfInventory("EnemyFlyingDeath",1,"DeathMissile3")
        goto DeathAnimation

    DeathAnimation:
        TNT1 A 0
        PIRT G 6 A_Scream
        PIRT H 6
        PIRT I 6 A_NoBlocking
        PIRT J 6
        PIRT K -1
        stop

    Death.Missile:
    Death.SuperMissile:
    Death.PowerCharged:
    Death.WaveCharged:
    Death.Bomb:
    Death.PowerBomb:
        PIRT G 2
        PIRT H 2 A_Scream
        PIRT I 2 A_NoBlocking
        TNT1 A 0 ThrustThingZ(0,random(20,40),0,1)
        TNT1 A 0 A_Recoil(random(5,8))
    DeathMissile2:
        PIRT I 1 A_GiveInventory("EnemyFlyingDeath")
        TNT1 A 0 A_CheckFloor("Death")
        loop
    DeathMissile3:
        PIRT J 2
        TNT1 A 0 A_PlaySound("pirate/land")
        PIRT K -1
        Stop

    Death.IceBeam:
    Death.IceCharged:
    Death.IceBeamExplode:
    Death.IceChargedExplode:
        PIRT G 1
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_PlaySound("misc/freeze")
        TNT1 A 0 A_PlaySoundEx("ibeam/freeze","soundslot6")
        TNT1 A 0 A_SpawnItemEx("EnemyDropSpawner",0,0,10,0,0,0,0,32)
        TNT1 A 0 A_SpawnItemEx("PirateguyFrozen",0,0,0,0,0,0,0,32)
        Stop
    }
}

actor PirateguyFrozen : FrozenDummy
{
    Scale 0.75
    // Translation "Ice" // NOPE THIS DOESN'T WORK
    States
    {
    Spawn:
        PIRT Z 350
        PIRT Z 1 A_Jump(128, "Death")
        Loop
    }
}

ACTOR PirateBolt
{
    Radius 6
    Height 8
    Speed 20
    Scale 0.35
    Damage (10)
    Projectile
    +RANDOMIZE
    RenderStyle Add
    Alpha 0.95
    DeathSound "pirate/hit"
    Decal HImpScorch
    States
    {
    Spawn:
        PSHT AABBCCDDEEFFGGHHIIJJKKLL 1 Bright A_SpawnItemEx("PirateBoltTrail",0,0,0,0,0,0,0,160)
        Loop
    Death:
        TNT1 A 0
        TNT1 A 0 A_Scream
        TNT1 A 0 A_SpawnItemEx("PirateBoltPuff",0,0,0,0,0,0,0,160)
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("PirateBoltPuff2",0,0,0,random(-300,300)/100.00,random(-300,300)/100.00,random(-300,300)/100.00,random(-180,180),160)
        TNT1 A 1
        Stop
    }
}

ACTOR PirateBoltPuff
{
    +CLIENTSIDEONLY
    +NOINTERACTION
    Scale 0.5
    Renderstyle Add
    Alpha 0.75
    States
    {
    Spawn:
        TNT1 A 0
        PHIT ABCDEFGHIJKLMNOP 1 Bright A_FadeOut(0.025)
        stop
    }
}

ACTOR PirateBoltTrail
{
    +CLIENTSIDEONLY
    +NOINTERACTION
    Scale 0.3
    Renderstyle Add
    Alpha 0.75
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(594,0,0,0) == 1,"Nope")
        PSHT ABCDEFGHIJKL 1 Bright A_FadeOut(0.05)
        stop
    Nope:
        TNT1 A 1
        stop
    }
}

ACTOR PirateBoltPuff2 : PirateBoltTrail
{
    +CLIENTSIDEONLY
    +NOINTERACTION
    Scale 0.1
    Renderstyle Add
    Alpha 0.7
}